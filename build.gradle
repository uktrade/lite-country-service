buildscript {
  repositories {
    maven {
      url 'https://nexus.ci.uktrade.io/repository/lite-build-and-proxy/'
    }
  }
}

plugins {
  id 'application'
  id 'java'
  id 'jacoco'
  id 'com.github.johnrengelman.shadow' version '1.2.3'
  id "au.com.dius.pact" version "3.3.10"
  id 'org.flywaydb.flyway' version '4.0.1'
}

apply plugin: 'idea'
apply plugin: 'maven-publish'

idea {
  module {
    inheritOutputDirs = false
    outputDir = file('build/classes/main')
    testOutputDir = file('build/classes/test')
  }
}

group 'uk.gov.bis.lite'

if (project.hasProperty('projVersion')) {
  project.version = project.projVersion
} else {
  project.version = "1.0-SNAPSHOT"
//  SimpleDateFormat sdf = new SimpleDateFormat("YYYYMMdd.HHmmss")
//  sdf.setTimeZone(new SimpleTimeZone(SimpleTimeZone.UTC_TIME, "UTC"))
//  project.version = sdf.format(new Date()) + "-SNAPSHOT"
}

mainClassName = "uk.gov.bis.lite.countryservice.CountryServiceApplication"

ext.dropwizardVersion = '1.2.1'

repositories {
  //mavenCentral()
  jcenter() //TODO MOVE TO DW COMMON
  maven {
    name "proxy"
    url "https://nexus.ci.uktrade.io/repository/lite-build-and-proxy/"
  }
  maven {
    name "snapshots"
    url "https://nexus.ci.uktrade.io/repository/maven-snapshots/"
  }
  maven {
    name "releases"
    url "https://nexus.ci.uktrade.io/repository/maven-releases/"
  }
}

//repositories {
//  mavenCentral()
//  jcenter() //TODO MOVE TO DW COMMON
//  maven {
//    name "snapshots"
//    url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/maven-snapshots/"
//  }
//  maven {
//    name "releases"
//    url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/maven-releases/"
//  }
//}

dependencies {
  compile "io.dropwizard:dropwizard-core:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-db:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-jdbi:$dropwizardVersion"
  compile "io.dropwizard:dropwizard-auth:$dropwizardVersion"
  compile 'ru.vyarus:dropwizard-guicey:3.2.0'
  compile 'org.quartz-scheduler:quartz:2.1.5'
  compile 'uk.gov.bis.lite:spire-client:1.6'
  compile 'net.logstash.logback:logstash-logback-encoder:4.10'
  compile 'ch.qos.logback.contrib:logback-jackson:0.1.5'
  compile 'uk.gov.bis.lite:json-console-appender:1.1'
  compile 'uk.gov.bis.lite:jersey-correlation-id:1.0'
  compile 'org.postgresql:postgresql:42.1.4'
  compile 'org.flywaydb:flyway-core:4.0.1'
  compile 'org.apache.commons:commons-collections4:4.1'
  compile 'uk.gov.bis.lite:readiness-metric:1.0'

  //TODO MOVE TO DW COMMON
  compile group: 'io.pivotal.labs', name: 'cf-env', version: '0.0.1'

  testCompile 'junit:junit:4.12'
  testCompile 'org.mockito:mockito-core:1.10.19'
  testCompile "io.dropwizard:dropwizard-testing:$dropwizardVersion"
  testCompile 'org.assertj:assertj-core:3.4.1'
  testCompile('com.github.tomakehurst:wiremock:2.7.1') {
    exclude group: 'org.mortbay.jetty', module: 'servlet-api'
  }
  testCompile 'org.skyscreamer:jsonassert:1.3.0'
  testCompile 'au.com.dius:pact-jvm-provider-junit_2.11:3.3.10'
  testCompile 'org.awaitility:awaitility:3.0.0'
  testCompile 'ru.yandex.qatools.embed:postgresql-embedded:2.6'
  testCompile 'com.h2database:h2:1.4.196'
}

flyway {
  url = 'jdbc:postgresql://localhost:5432/postgres?currentSchema=countrysvc'
}

shadowJar {
  classifier = ''
  mergeServiceFiles()
}

jar {
  manifest {
    attributes 'Main-Class': mainClassName
  }
}

build {
  dependsOn shadowJar
}


run { args 'server', 'config.yaml' }

publishing {
  repositories {
    maven {
      name "snapshots"
      url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/maven-snapshots/"
    }
    maven {
      name "releases"
      url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/maven-releases/"
    }
    maven {
      name "lite-builds"
      url "https://nexus.ci.uktrade.io/repository/lite-builds/"
      credentials {
        username 'admin'
        password 'admin123'
      }
    }
  }
}

task apiJar(type: Jar, dependsOn: compileJava) {
  from sourceSets.main.output.classesDir
  include 'uk/gov/bis/lite/countryservice/api/**/*'
}

task apiJarWithSources(type: Jar) {
  from sourceSets.main.allJava
  include 'uk/gov/bis/lite/countryservice/api/**/*'
}

publishing {
  publications {
    api(MavenPublication) {
      artifactId 'lite-country-service-api'
      version '1.2'
      artifact apiJar
      artifact apiJarWithSources {
        classifier "sources"
      }
    }
    service(MavenPublication) {
      artifactId 'lite-country-service'
      from components.shadow
    }
  }
}
